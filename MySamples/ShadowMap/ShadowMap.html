<!doctype html>
<html>
	<head>
		<title>ShadowMap</title>
		<script src="./gl-matrix.js"></script>
		<script src="./model-data.js"></script>
		<script src="./mouse-drag.js"></script>
		<script src="./main.js"></script>

		<script id="shadowmap-shader-vs" type="x-shader/x-vertex">
		uniform mat4 modelMatrix;
		uniform mat4 viewMatrix;
		uniform mat4 projectionMatrix;

		attribute vec3 pos;

		void main()
		{
			gl_Position = projectionMatrix * viewMatrix * modelMatrix * vec4(pos, 1);
		}
		</script>


		<script id="color-shader-vs" type="x-shader/x-vertex">
		uniform mat4 modelMatrix;
		uniform mat4 invTransModelMatrix;
		uniform mat4 viewMatrix;
		uniform mat4 projectionMatrix;

		attribute vec3 pos;
		attribute vec3 normal;

		varying vec3 fsNormal;
		varying vec3 fsPos;

		void main()
		{
			vec4 worldPos = modelMatrix * vec4(pos, 1);
			gl_Position = projectionMatrix * viewMatrix * worldPos;
			fsPos = worldPos.xyz;
			fsNormal = (invTransModelMatrix * vec4(normal, 0)).xyz;
		}
		</script>

		<script id="shadowmap-shader-fs" type="x-shader/x-fragment">
		precision mediump float;

		void main()
		{
			float r = floor(gl_FragCoord.z * 255.0);
			float g = floor((gl_FragCoord.z * 255.0 - r) * 255.0);
			float b = floor(((gl_FragCoord.z * 255.0 - r) * 255.0 - g) * 255.0);
			gl_FragColor = vec4(r / 255.0, g / 255.0, b / 255.0, 1);
		}
		</script>

		<script id="color-shader-fs" type="x-shader/x-fragment">
		precision mediump float;

		uniform vec3 meshColor;
		uniform vec3 lightDirection;
		uniform sampler2D sampler;
		uniform mat4 shadowmapViewProjectionMatrix;

		varying vec3 fsNormal;
		varying vec3 fsPos;

		void main()
		{
			vec4 hPos = shadowmapViewProjectionMatrix * vec4(fsPos, 1);
			float s = hPos.x / hPos.w / 2.0 + 0.5;
			float t = hPos.y / hPos.w / 2.0 + 0.5;
			float z = hPos.z / hPos.w / 2.0 + 0.5;
			vec4 shadowmapRaw = texture2D(sampler, vec2(s, t));
			float shadow = float(shadowmapRaw.r + shadowmapRaw.g / 255.0 + shadowmapRaw.b / 255.0 / 255.0 + 0.001 >= z);
			vec3 diffuse = (clamp(dot(normalize(fsNormal), normalize(lightDirection)) * shadow, 0.0, 1.0) * 0.8 + 0.2) * meshColor;
			gl_FragColor = vec4(diffuse, 1.0);
		}
		</script>
	</head>

	<body onload="start()">
	    <canvas id="glcanvas" width="800" height="600">
			Your browser doesn't appear to support the <code>&lt;canvas&gt;</code> element.
    	</canvas>
		<ul>
			<li id="labelFps">FPS: 0</li>
		</ul>
    </body>
</html>
